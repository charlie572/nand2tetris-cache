// 64 byte cache lines => 4 word lines
// 512 bytes of cache => 8 locations

CHIP SetAssociativeCache {
    IN in[16], addressIn[15], loadIn, memoryIn[16];
    OUT out[16], finished, addressOut[15], memoryOut[16], loadOut;

    PARTS:
    // check for hit
    Equal16(a[0..9]=tag1, b[0..9]=addressIn[5..14], out=tagCheck1);
    And(a=tagCheck1, b=valid1, out=hit1);

    Equal16(a[0..9]=tag2, b[0..9]=addressIn[5..14], out=tagCheck2);
    And(a=tagCheck2, b=valid2, out=hit2);

    Or(a=hit1, b=hit2, out=hit);
    Not(in=hit, out=miss);

    // output data
    Not(in=loadIn, out=notLoadIn);
    And(a=hit, b=notLoadIn, out=outputData);
    Mux16(a=false, b=data, sel=outputData, out=out);

    // cache miss
    And(a=miss, b=NoLoadOrWritten, out=readingLine);
    PC(inc=readingLine, reset=endCacheMiss, out[0..1]=wordSelect);
    DMux4Way(in=miss, sel=wordSelect, a=dataLoad00, b=dataLoad10, c=dataLoad20, d=dataLoad30);
    Equal16(a[0..1]=true, b[0..1]=wordSelect, out=endCacheMiss);

    // write through
    DMux4Way(in=UnprocessedWrite, sel=addressIn[0..1], a=dataLoad01, b=dataLoad11, c=dataLoad21, d=dataLoad31);

    Mux16(a=in, b=false, sel=NoLoadOrWritten, out=memoryOut);

    DFF(in=loadIn, out=written);

    Or(a=notLoadIn, b=written, out=NoLoadOrWritten);
    Not(in=NoLoadOrWritten, out=UnprocessedWrite, out=loadOut);
    And(a=hit, b=NoLoadOrWritten, out=finished);

    // addressOut
    Mux16(a[0..1]=wordSelect, a[2..14]=addressIn[2..14], b=false, sel=hit, out=gatedReadMissAddress);
    Mux16(a=gatedReadMissAddress, b[0..14]=addressIn, sel=UnprocessedWrite, out[0..14]=addressOut);

    




    // ******************* memory ********************

    // cache data
    Or(a=dataLoad00, b=dataLoad01, out=dataLoad0);
    Or(a=dataLoad10, b=dataLoad11, out=dataLoad1);
    Or(a=dataLoad20, b=dataLoad21, out=dataLoad2);
    Or(a=dataLoad30, b=dataLoad31, out=dataLoad3);

    DMux(in=dataLoad0, sel=valid1, a=dataLoada0, b=dataLoadb0);
    DMux(in=dataLoad1, sel=valid1, a=dataLoada1, b=dataLoadb1);
    DMux(in=dataLoad2, sel=valid1, a=dataLoada2, b=dataLoadb2);
    DMux(in=dataLoad3, sel=valid1, a=dataLoada3, b=dataLoadb3);

    Mux16(a=memoryIn, b=in, sel=UnprocessedWrite, out=cacheDataIn);

    RAM8(in=cacheDataIn, load=dataLoada0, address=addressIn[2..4], out=outDataA0);
    RAM8(in=cacheDataIn, load=dataLoada1, address=addressIn[2..4], out=outDataA1);
    RAM8(in=cacheDataIn, load=dataLoada2, address=addressIn[2..4], out=outDataA2);
    RAM8(in=cacheDataIn, load=dataLoada3, address=addressIn[2..4], out=outDataA3);

    RAM8(in=cacheDataIn, load=dataLoadb0, address=addressIn[2..4], out=outDataB0);
    RAM8(in=cacheDataIn, load=dataLoadb1, address=addressIn[2..4], out=outDataB1);
    RAM8(in=cacheDataIn, load=dataLoadb2, address=addressIn[2..4], out=outDataB2);
    RAM8(in=cacheDataIn, load=dataLoadb3, address=addressIn[2..4], out=outDataB3);

    Mux4Way16(a=outDataA0, b=outDataA1, c=outDataA2, d=outDataA3, sel=addressIn[0..1], out=data1);
    Mux4Way16(a=outDataB0, b=outDataB1, c=outDataB2, d=outDataB3, sel=addressIn[0..1], out=data2);
    Mux16(a=data1, b=data2, sel=hit2, out=data);

    // tags
    DMux(in=endCacheMiss, sel=valid1, a=loadTag1, b=loadTag2);
    RAM8(in[0..9]=addressIn[5..14], load=endCacheMiss, address=addressIn[2..4], out[0..9]=tag1);
    RAM8(in[0..9]=addressIn[5..14], load=endCacheMiss, address=addressIn[2..4], out[0..9]=tag2);

    // valid bits
    DMux(in=true, sel=valid1, a=setValid1, b=setValid2);
    RAM8(in[0]=true, load=setValid1, address=addressIn[2..4], out[0]=valid1);
    RAM8(in[0]=true, load=setValid2, address=addressIn[2..4], out[0]=valid2);
}
